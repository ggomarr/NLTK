% start S
# ###################
# Grammar Productions
# ###################
# S expansion productions
S -> NP[NUM=?n] VP[NUM=?n]
# NP expansion productions
NP[NUM=?n] -> PropN[NUM=?n]
NP[NUM=?n] -> Det[NUM=?n] N[NUM=?n]
NP[NUM=sg] -> N[NUM=sg, -COUNT]
NP[NUM=pl] -> N[NUM=pl]
# VP expansion productions
VP[TENSE=?t, NUM=?n] -> IV[TENSE=?t, NUM=?n]
VP[TENSE=?t, NUM=?n] -> TV[TENSE=?t, NUM=?n] NP
VP[TENSE=?t, NUM=?n] -> CV[TENSE=?t, NUM=?n] ADJ
# ###################
# Lexical Productions
# ###################
Det[NUM=sg] -> 'this' | 'every' | 'a'
Det[NUM=pl] -> 'these' | 'all'
Det -> 'the' | 'some' | 'several'
PropN[NUM=sg]-> 'Kim' | 'Jody'
N[NUM=sg, +COUNT] -> 'dog' | 'girl' | 'car' | 'child' | 'boy' | 'song'
N[NUM=sg, -COUNT] -> 'water'
N[NUM=pl] -> 'dogs' | 'girls' | 'cars' | 'children' | 'boys' | 'songs'
IV[TENSE=pres,  NUM=sg] -> 'disappears' | 'walks'
TV[TENSE=pres, NUM=sg] -> 'sees' | 'likes' | 'sings'
CV[TENSE=pres, NUM=sg] -> 'is' | 'seems'
IV[TENSE=pres,  NUM=pl] -> 'disappear' | 'walk'
TV[TENSE=pres, NUM=pl] -> 'see' | 'like' | 'sing'
CV[TENSE=pres, NUM=pl] -> 'are' | 'seem'
IV[TENSE=past] -> 'disappeared' | 'walked'
TV[TENSE=past] -> 'saw' | 'liked' | 'sang'
CV[TENSE=past] -> 'were' | 'seemed'
ADJ -> 'precious' | 'happy'
